class Ball {
    field int x, y;
    field int r; //radios.
    field int vx, vy;
    field int gravityCounter;

    constructor Ball new(int startX, int startY) {
        let x = startX; //Set x.
        let y = startY; //Set y.
        let r = 5; //Radius.
        let vx = 0; //Horizontal speed.
        let vy = 0; //Vertical speed.
        let gravityCounter = 0; //Gravity timer.
        return this;
    }

    //Free object.
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    //Draw ball on the screen.
    method void draw() {
        //Within the screen.
        if ((x > 6) & (x < 505) & (y > 6) & (y < 249)) {
            do Screen.setColor(true); //Set black.
            do Screen.drawCircle(x, y, r);
        }
        return;
    }

    /** Erases the ball */
    method void erase() {
        //Within the screen.
        if ((x > 6) & (x < 505) & (y > 6) & (y < 249)) {
            do Screen.setColor(false);//Set white.
            do Screen.drawCircle(x, y, r);
        }
        return;
    }

    //Set ball position.
    method void setPosition(int newX, int newY) {
        do erase();//Erase current ball.
        let x = newX;
        let y = newY;
        let vx = 0;
        let vy = 0;
        do draw();//Draw new ball.
        return;
    }
    //Move the ball to the left.
    method void shiftLeft() {
        do erase();
        let x = x - 4;
        if (x < 10) { let x = 10; }
        do draw();
        return;
    }
    //Move the ball to the right.
    method void shiftRight() {
        do erase();
        let x = x + 4;
        if (x > 400) { let x = 400; }
        do draw();
        return;
    }
    //Sets speed based on a power value.
    method void shoot(int power) {
        let vx = 7 + (power / 15);
        let vy = -((power / 6) + 3);
        return;
    }

    //Updates ball position with SOLID BACKBOARD logic.
    method boolean move() {
        do erase();
        let x = x + vx;
        let y = y + vy;

        //Apply Gravity
        let gravityCounter = gravityCounter + 1;
        if (gravityCounter > 1) {
            let vy = vy + 1;
            let gravityCounter = 0;
        }

        //COLLISIONS
        //1.SOLID BACKBOARD LOGIC (The Fix).
        // Board area: X between 453 and 468, Y between 70 and 130
        if ((x > 450) & (x < 470)) {
            if ((y > 70) & (y < 130)) {

                // Case A: Hit from FRONT (moving Right)
                if (vx > 0) {
                    let vx = -vx;
                    let vx = vx / 2;   // Lose energy
                    let x = 450;       // Push Left (Stay in court)
                }
                else {
                    // Case B: Hit from BEHIND (moving Left)
                    if (vx < 0) {
                        let vx = -vx;
                        let vx = vx / 2;
                        let x = 470;   // Push Right (Stay outside)
                    }
                }
            }
        }

        //2.Ceiling Bounce.
        if (y < (r + 2)) { let y = r + 2; let vy = -vy; }

        //3.Left Wall Bounce.
        if (x < (r + 2)) { let x = r + 2; let vx = -vx; }

        //4.Right Wall Bounce.
        if (x > 505)     { let x = 505;   let vx = -vx; }

        //5.Floor (Stop moving).
        if (y > 235) {
            let y = 235;
            let vx = 0;
            let vy = 0;
            do draw();
            return false; // Stopped
        }

        do draw();
        return true;
    }

    method int getX() {
        return x;
    }
    method int getY() {
        return y;
    }
}