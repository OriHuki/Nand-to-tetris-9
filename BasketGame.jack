class BasketGame {
    field Ball ball;
    field int score;
    field int timer;
    field int power; //Shot strength.
    field int potentialPoints; //Points for a shot.
    field boolean powerGoingUp; //Power going up?.
    field int seed;

    //variables.
    field String msgSwish;
    field String msgClear;

    constructor BasketGame new() {
        //BALL position start at - (40, 200).
        let ball = Ball.new(40, 200);
        let score = 0;
        let seed = 1234;

        let msgSwish = "SWISH! +";
        let msgClear = "          ";

        return this;
    }
    //Clean memory in the end od the game.
    method void dispose() {
        do ball.dispose(); //Erase ball.
        do msgSwish.dispose(); //dispose string.
        do msgClear.dispose(); //dispose string.
        do Memory.deAlloc(this);
        return;
    }
    //Int random.
    method int randRange(int low, int high) {
        var int range;
        var int res;
        let range = high - low; //Length of the range.
        let seed = (seed * 237) + 51;
        if (seed < 0) { let seed = -seed; } //If seed neg make seed pos.
        let res = seed - (range * (seed / range));
        return low + res;
    }
    //Opening screen till the player press space.
    method void showOpeningScreen() {
        var char key;
        do Screen.clearScreen();

        do Output.moveCursor(5, 19);
        do Output.printString("Be an NBA player for 1 day!");
        do Output.moveCursor(6, 19);
        do Output.printString("--------------------------");
        do Output.moveCursor(9, 21);
        do Output.printString("Move: [Left] / [Right]");
        do Output.moveCursor(11, 25);
        do Output.printString("Shoot: [SPACE]");
        do Output.moveCursor(14, 24);
        do Output.printString("Time: 60 Seconds");
        do Output.moveCursor(17, 27);
        do Output.printString("Good Luck!");
        do Output.moveCursor(20, 4);
        do Output.printString("Created by: Ori Hukima, Liel Uzan, Tzipora Feuchtwanger");
        do Output.moveCursor(22, 21);
        do Output.printString("Press [SPACE] to Start");

        let key = 0;
        while (~(key = 32)) {
            let key = Keyboard.keyPressed();
            let seed = seed + 1;
            if (seed > 30000) { let seed = 0; }
        }
        return;
    }

    //Gameover screen.
    method void showGameOver() {
        //Clean screen.
        do Screen.clearScreen();


        do Screen.drawRectangle(130, 70, 380, 190);
        do Screen.setColor(false);
        do Screen.drawRectangle(132, 72, 378, 188);
        do Screen.setColor(true);

        do Output.moveCursor(9, 27);
        do Output.printString("TIME'S UP!");

        do Output.moveCursor(11, 24);
        do Output.printString("Final Score: ");
        do Output.printInt(score);


        if (score > 10) {
            do Output.moveCursor(14, 24);
            do Output.printString("You are a Star!");
        }
        else {
            if (score > 4) {
                do Output.moveCursor(14, 27);
                do Output.printString("Well Done!");
            }
            else {
                do Output.moveCursor(14, 25);
                do Output.printString("Practice more.");
            }
        }
        return;
    }



    // Shows: 1. time left. 2. current score. 3. potentialPoints.
    method void updateHUD() {
        var int secondsLeft;
        let secondsLeft = timer / 40;

        do Output.moveCursor(0, 9);
        do Output.printInt(score);

        do Output.moveCursor(1, 9);
        do Output.printInt(secondsLeft);

        do Output.moveCursor(0, 33);
        do Output.printInt(potentialPoints);

        return;
    }

    //Draw court.
    method void drawCourt() {
        do Screen.setColor(true);
        do Screen.drawLine(0, 240, 511, 240);
        return;
    }

    //Draw basket.
    method void drawFullBasket() {
        do Screen.setColor(true);
        do Screen.drawRectangle(495, 160, 511, 240);
        do Screen.drawLine(495, 160, 460, 120);
        do Screen.drawLine(495, 170, 460, 130);
        do Screen.drawLine(495, 165, 460, 125);
        do Screen.drawRectangle(458, 70, 462, 130);
        do Screen.drawLine(425, 105, 458, 105);
        do Screen.drawLine(425, 105, 432, 125);
        do Screen.drawLine(458, 105, 451, 125);
        do Screen.drawLine(432, 125, 451, 125);
        do Screen.drawLine(433, 105, 438, 125);
        do Screen.drawLine(441, 105, 446, 125);
        do Screen.drawLine(449, 105, 451, 115);
        do Screen.drawLine(435, 125, 440, 105);
        do Screen.drawLine(443, 125, 448, 105);
        do Screen.drawLine(451, 125, 456, 105);
        return;
    }

    //Draw basket and court.
    //Update score, time, value.
    //Clean screen.
    method void drawEnvironment() {
        do Screen.clearScreen();
        do drawCourt();
        do drawFullBasket();

        do Output.moveCursor(0, 2);
        do Output.printString("Score: ");

        do Output.moveCursor(1, 2);
        do Output.printString("Time : ");

        do Output.moveCursor(0, 26);
        do Output.printString("Value: ");
        do Output.moveCursor(0, 36);
        do Output.printString(" pts");

        do updateHUD();
        return;
    }

    //Showing the shot strength meter.
    method void drawPowerMeter() {
        do Screen.setColor(false);
        do Screen.drawRectangle(10, 100, 30, 200);
        do Screen.setColor(true);
        do Screen.drawLine(10, 100, 30, 100);
        do Screen.drawLine(10, 200, 30, 200);
        do Screen.drawLine(10, 100, 10, 200);
        do Screen.drawLine(30, 100, 30, 200);
        do Screen.drawRectangle(12, 200 - power, 28, 200);
        return;
    }

    //Delete shot strength meter.
    method void erasePowerMeter() {
        do Screen.setColor(false);
        do Screen.drawRectangle(10, 100, 31, 201);
        return;
    }

    //Calc points.
    method void calculateZone() {
        var int bx;
        let bx = ball.getX();
        if (bx < 180) { let potentialPoints = 3; }
        else {
            if (bx < 320) { let potentialPoints = 2; }
            else { let potentialPoints = 1; }
        }
        return;
    }

    //Game running.
    method void run() {
        var char key;
        var boolean positionLocked;
        var boolean powerLocked;
        var boolean moving;
        var boolean isGoal;
        var boolean wasAboveRim;
        var int randomX;

        do showOpeningScreen();

        let timer = 2400;
        let score = 0;
        do drawEnvironment();

        while (timer > 0) {

            let randomX = randRange(20, 380);
            do ball.setPosition(randomX, 200);
            do drawCourt();

            let power = 10;
            let powerGoingUp = true;
            let positionLocked = false;
            let powerLocked = false;
            let isGoal = false;
            let wasAboveRim = false;

            while ((~positionLocked) & (timer > 0)) {
                let key = Keyboard.keyPressed();
                if (key = 130) {
                    do ball.shiftLeft();
                    if (ball.getX() < 200) { do drawCourt(); }
                }
                if (key = 132) {
                    do ball.shiftRight();
                }
                if (key = 32)  { let positionLocked = true; }
                do calculateZone();

                let timer = timer - 1;
                if ((timer & 15) = 0) { do updateHUD(); }
                do Sys.wait(20);
            }

            while ((~(key = 0)) & (timer > 0)) {
                let key = Keyboard.keyPressed();
                let timer = timer - 1;
                do Sys.wait(5);
            }


            while ((~powerLocked) & (timer > 0)) {
                let key = Keyboard.keyPressed();
                if (powerGoingUp) {
                    let power = power + 4;
                    if (power > 95) { let powerGoingUp = false; }
                } else {
                    let power = power - 4;
                    if (power < 5) { let powerGoingUp = true; }
                }
                do drawPowerMeter();
                if (key = 32) { let powerLocked = true; }
                let timer = timer - 1;
                if ((timer & 15) = 0) { do updateHUD(); }
                do Sys.wait(20);
            }

            do erasePowerMeter();

            if (timer > 0) {
                do ball.shoot(power);
                let moving = true;

                while (moving & (timer > 0)) {
                    let moving = ball.move();

                    if (ball.getX() > 300) { do drawFullBasket(); }
                    if (ball.getY() > 180) { do drawCourt(); }

                    do ball.draw();

                    if ((ball.getY() < 100) & (ball.getX() > 428) & (ball.getX() < 455)) {
                        let wasAboveRim = true;
                    }

                    if (wasAboveRim) {
                        if ((ball.getY() > 105) & (ball.getX() > 428) & (ball.getX() < 455)) {
                            let isGoal = true;
                            let wasAboveRim = false;
                        }
                    }

                    let timer = timer - 1;
                    if ((timer & 15) = 0) { do updateHUD(); }
                    do Sys.wait(20);
                }

                if (isGoal) {
                    let score = score + potentialPoints;
                    do Output.moveCursor(5, 25);
                    do Output.printString(msgSwish);
                    do Output.printInt(potentialPoints);

                    do Sys.wait(500);
                    let timer = timer - 20;

                    do Output.moveCursor(5, 25);
                    do Output.printString(msgClear);
                }
            }
        }

        do showGameOver();
        return;
    }
}